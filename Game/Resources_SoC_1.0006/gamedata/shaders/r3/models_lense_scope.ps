#include "common.h"

//uniform float4x4 m_script_params; //Не используется, зарезервировано на будущее.

uniform	float4 m_hud_params; // zoom_rotate_factor, secondVP_zoom_factor, NULL, NULL

// KRodin: TODO: Вынести в common?
uniform	float4 screen_res;

#include "pnv.h"

uniform	float4 m_blender_mode; // x = [0 - default, 1 - night vision, 2 - thermal vision]; y = [0.0f / 1.0f - происходит ли в данный момент рендеринг картинки для прицела]; z = [0.0f / 1.0f - выключен или включён двойной рендер]; w - зарезервировано на будущее.

// Активен-ли двойной рендер?
inline bool isSecondVPActive()
{
	return (m_blender_mode.z == 1.f);
}

// Рендерится ли в данный момент кадр для прицела?
inline bool IsSVPFrame()
{
	return (m_blender_mode.y == 1.f);
}

Texture2D s_vp2;

struct 	v2p
{
 	float2 	tc0: 		TEXCOORD0;	// base
 	float3 	tc1: 		TEXCOORD1;	// environment
  	float4	c0:			COLOR0;		// sun.(fog*fog)
};

//////////////////////////////////////////////////////////////////////////////////////////

float resize(float input, float factor, float offset)
{
	return (input - 0.5f + offset) / factor + 0.5f - offset;
}

float4 main( v2p I ) : SV_Target
{
	float4 t_base = s_base.Sample(smp_base, I.tc0); // Текстура сетки	

	// Растягиваем картинку в линзе так, чтобы на любом разрешении экрана были правильные пропорции
	I.tc0.x = resize(I.tc0.x, screen_res.x / screen_res.y, 0);
	float4 t_vp2 = s_vp2.Sample(smp_base, I.tc0); // Изображение со второго вьюпорта

	if (!isSecondVPActive()) {
		t_vp2.rgb /= 100; //KRodin: чтобы изображение во втором вьюпорте не застывало после его отключения - просто затемним его в 100 раз.
	} else if (m_blender_mode.x == 1.f) { //Зеленый ПНВ
		t_vp2.rgb = calc_night_vision_effect(I.tc0, t_vp2, float3(0.66, 2.0, 0.5));
	} else if (m_blender_mode.x == 2.f) { //Фейковый тепловизор, на самом деле ПНВ с красным оттенком
		t_vp2.rgb = calc_night_vision_effect(I.tc0, t_vp2, float3(0.79, 0.191, 0.247));
	}

	// Миксуем всё и собираем финальную картинку
	float3 final = lerp(t_vp2, t_base, t_base.a); // Сетку с вьюпортом

	// out
	return float4(final.r, final.g, final.b, m_hud_params.x);
}
