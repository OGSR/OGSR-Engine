#include "common.h"
#include "lmodel.h"

#ifdef USE_MINMAX_SM
#define SM_MINMAX
#endif

#if SUN_QUALITY > 2
#define ULTRA_SHADOWS_ON
#endif //	SUN_QUALITY>2

#ifdef ULTRA_SHADOWS_ON
#define USE_ULTRA_SHADOWS
#endif

#include "shadow.h"

// SSS Implementation
#include "screenspace_shadows.h"

#ifdef USE_SUNFILTER
#ifdef MSAA_OPTIMIZATION
float4 main(v2p_volume I, uint iSample : SV_SAMPLEINDEX) : SV_Target
#else
float4 main(v2p_volume I) : SV_Target
#endif
{
    gbuffer_data gbd = gbuffer_load_data(GLD_P(I.tc, I.hpos, ISAMPLE));

#ifdef GBUFFER_OPTIMIZATION
    //	Emulate virtual offset
    gbd.P += gbd.N * 0.015f;
#endif //	GBUFFER_OPTIMIZATION

    float4 _P = float4(gbd.P, 1.0);

    float4 PS = mul(m_shadow, _P);

    float s = shadowtest_sun(PS, I.tcJ) * sunmask(_P);

    return s;
}
#else
#ifdef MSAA_OPTIMIZATION
float4 main(v2p_volume I, uint iSample : SV_SAMPLEINDEX) : SV_Target
#else
float4 main(v2p_volume I) : SV_Target
#endif
{
    gbuffer_data gbd = gbuffer_load_data(GLD_P(I.tc.xy / I.tc.w, I.hpos, ISAMPLE));

#ifdef GBUFFER_OPTIMIZATION
    //	Emulate virtual offset
    gbd.P += gbd.N * 0.015f;
#endif //	GBUFFER_OPTIMIZATION

    float4 _P = float4(gbd.P, gbd.mtl);
    float4 _N = float4(gbd.N, gbd.hemi);
    float4 _C = float4(gbd.C, gbd.gloss);

    // ----- light-model
    float m = xmaterial;
#ifndef USE_R2_STATIC_SUN
    m = _P.w;
#endif
    float4 light = plight_infinity(m, _P, _N, _C, Ldynamic_dir);

    // ----- shadow
    float4 P4 = float4(_P.x, _P.y, _P.z, 1.0);
    float4 PS = mul(m_shadow, P4);
    float s = sunmask(P4);
    s *= shadow(PS);

    s *= SSFX_ScreenSpaceShadows(_P, I.hpos, ISAMPLE);

#ifdef SSFX_ENHANCED_SHADERS // We have Enhanced Shaders installed
    float3 result = SRGBToLinear(s);
    result.rgb *= light * SRGBToLinear(Ldynamic_color.rgb);
    return float4(result.rgb, 0);
#else
    return float4(Ldynamic_color * light * s);
#endif
}
#endif